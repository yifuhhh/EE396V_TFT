####################################
### TRANSFER CURVE OF a-IGZO TFT ###
####################################

go atlas 

### Local variables
set ch_L=50
set spacing=100

### Set up Mesh and Geometry
# ".str" files are structural files and map (for one operation point)
# all the unknowns of your device.
mesh width=180 outf=tftex10_1.str master.out

x.m l=-$ch_L/$spacing 		s=$ch_L/$spacing 
x.m l= $ch_L+$ch_L/$spacing  	s=$ch_L/$spacing 
y.m l=0 	s=0.2/20
y.m l=0.2 	s=0.2/50
y.m l=0.4 	s=0.2/3

region num=1 material=igzo y.min=0    y.max=0.2
region num=2 material=sio2 y.min=0.2  y.max=0.4

### Define Boundary Conditions
#
elec num=1 name=gate bottom
elec num=2 name=source y.max=0.0 x.min=-$ch_L/$spacing   x.max=0
elec num=3 name=drain  y.max=0.0 x.min=$ch_L x.max=$ch_L+$ch_L/$spacing 
#
# We define the gate as N.POLY.  This pins the gate workfunction
# to the conduction band edge of silicon.
#
contact num=1 n.poly
#
# We also define a workfunction for the source and drain t
# is very close to the conduction edge.  In the reference the
# authors observed that without a workfunction the results for
# ohmic boundaries were not significantly different than the
# Schottky model.
#
contact num=2 workf=4.33
contact num=3 workf=4.33



### Define Material Parameters and Models
#
models fermi
#
# Key to the characterization of amorphous materials is the
# definition of the states within the band gap.
#
defects nta=1.55e21 ntd=1.55e21 wta=0.013 wtd=0.12 \
	nga=0.0 ngd=6.5e18 egd=2.0 wgd=0.1 \
	sigtae=1e-15 sigtah=1e-15 sigtde=1e-15 sigtdh=1e-15 \
	siggae=1e-15 siggah=1e-15 siggde=1e-15 siggdh=1e-15 \
	dfile=bulk_don.dat afile=bulk_acc.dat numa=64 numd=64

intdefect s.i intmaterial="igzo/sio2" continuous tfile=int_both.dat numa=24 numd=24  \
  nta=2e13 ntd=4e16 wta=0.19 wtd=0.1 \
  nga=0 ngd=0 ega=0.62 egd=0.78 wga=0.15 wgd=0.15 \
  sigtae=1.e-14 sigtah=1.e-14 sigtde=1.e-14 sigtdh=1.e-14 \  
  siggae=1.e-14 siggah=1.e-14 siggde=1.e-14 siggdh=1.e-14  

# Solve for Transfer Curve (Id-Vg)
#
solve init
solve prev
solve vdrain=0.1
solve vdrain=4
solve vdrain=10
save outf=transfer_10.str
log outf=transfer_10_neg.log csvfile=transfer_10_neg.csv
solve vgate=0 vstep=-0.2 vfinal=-10 name=gate
log off

load inf=transfer_10.str master
solve prev
log outf=transfer_10_pos.log csvfile=transfer_10_pos.csv
solve vstep=0.2 vfinal=20.0 name=gate
log off

solve init
solve prev
solve vgate=0.1
solve vgate=5
solve vgate=10
log outf=output.log csvfile=output.csv
solve vstep=0.2 vfinal=15.0 name=drain
log off

tonyplot -overlay transfer_10_neg.log transfer_10_pos.log
tonyplot output.log 
tonyplot -overlay bulk_don.dat bulk_acc.dat
tonyplot int_both.dat
tonyplot transfer_10.str

extract init infile="transfer_10_pos.log"
extract name="vt" (xintercept(maxslope(curve(abs(v."gate"),abs(i."drain")))) - abs(ave(v."drain"))/2.0) 
extract init infile="transfer_10_neg.log"
extract name="subvt" 1/slope(minslope(curve(abs(v."gate"),log10(abs(i."drain")),x.min=0 x.max=1.2)))

quit
